ext {
  componentName='Interlok OAUTH/Google Cloud'
  delombokTargetDir = new File("${project.projectDir}/src/main/generated")
}

dependencies {
  compile "com.google.auth:google-auth-library-oauth2-http:0.22.0"
  compile "com.google.guava:guava:30.0-jre"
  compile "org.apache.httpcomponents:httpclient:4.5.13"
  compile "commons-codec:commons-codec:1.15"
  compile "com.fasterxml.jackson.core:jackson-core:2.12.0"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task examplesJar(type: Jar, dependsOn: test) {
    classifier = 'examples'
    from new File(buildDir, '/examples')
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
artifacts {
    archives javadocJar
    archives examplesJar
    archives sourcesJar
}
jar {
  manifest {
    attributes("Built-By": System.getProperty('user.name'),
              "Build-Jdk": System.getProperty('java.version'),
              "Implementation-Title": componentName,
              "Implementation-Version": project.version,
              "Implementation-Vendor-Id": project.group,
              "Implementation-Vendor": organizationName)
  }
}
publishing {
  publications {
      mavenJava(MavenPublication) {
          from components.java

          artifact javadocJar { classifier "javadoc" }
          artifact examplesJar { classifier "examples" }
          artifact sourcesJar { classifier "sources" }
          pom.withXml {
            asNode().appendNode("name", componentName)
            asNode().appendNode("description", "Retrieving OAUTH access tokens from Google Cloud")
            asNode().appendNode("url", "http://interlok.adaptris.net/interlok-docs/cookbook-http-client.html#oauth")
            def properties = asNode().appendNode("properties")
            properties.appendNode("target", "3.6.5+")
            properties.appendNode("license", "false")
            properties.appendNode("tags", "oauth,google,gcloud")
          }
      }
  }
  repositories {
    maven {
      credentials {
        username repoUsername
        password repoPassword
      }
      url mavenPublishUrl
    }
  }
}
delombok {
  target = delombokTargetDir
}

task deleteGeneratedFiles(type: Delete) {
  delete delombokTargetDir
}

clean.dependsOn deleteGeneratedFiles