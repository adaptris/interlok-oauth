ext {
  componentName='Interlok OAUTH/Generic'
  delombokTargetDir = new File("${project.projectDir}/src/main/generated")
}

dependencies {
  compile ("com.adaptris:interlok-apache-http:$interlokCoreVersion") { changing= true}
  compile ("com.jayway.jsonpath:json-path:2.4.0")
  compile ("com.fasterxml.jackson.core:jackson-databind:2.12.1")
  testCompile ("io.undertow:undertow-core:2.2.3.Final")
 }

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task examplesJar(type: Jar, dependsOn: test) {
    classifier = 'examples'
    from new File(buildDir, '/examples')
}
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
artifacts {
    archives javadocJar
    archives examplesJar
    archives sourcesJar
}
jar {
  manifest {
    attributes("Built-By": System.getProperty('user.name'),
              "Build-Jdk": System.getProperty('java.version'),
              "Implementation-Title": componentName,
              "Implementation-Version": project.version,
              "Implementation-Vendor-Id": project.group,
              "Implementation-Vendor": organizationName)
  }
}
publishing {
  publications {
      mavenJava(MavenPublication) {
          from components.java

          artifact javadocJar { classifier "javadoc" }
          artifact examplesJar { classifier "examples" }
          artifact sourcesJar { classifier "sources" }
          pom.withXml {
            asNode().appendNode("name", componentName)
            asNode().appendNode("description", "Handling OAUTH where the provider does not have an explicit API")
            asNode().appendNode("url", "http://interlok.adaptris.net/interlok-docs/cookbook-http-client.html")
            def properties = asNode().appendNode("properties")
            properties.appendNode("target", "3.8.1+")
            properties.appendNode("license", "false")
            properties.appendNode("tags", "oauth")
          }
      }
  }
  repositories {
    maven {
      credentials {
        username repoUsername
        password repoPassword
      }
      url mavenPublishUrl
    }
  }
}
delombok {
  target = delombokTargetDir
}

task deleteGeneratedFiles(type: Delete) {
  delete delombokTargetDir
}

clean.dependsOn deleteGeneratedFiles
