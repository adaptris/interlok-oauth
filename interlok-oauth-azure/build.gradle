ext {
  componentName='Interlok OAUTH/Azure'
  componentDesc='Retrieving OAUTH access tokens from Azure'
  delombokTargetDir = new File("${project.projectDir}/src/main/generated")
}

dependencies {
  api ("com.microsoft.azure:adal4j:1.6.7") {
    exclude group: "com.google.code.gson", module: "gson"
  }
  implementation ("com.google.code.gson:gson:2.11.0")
  api "net.minidev:json-smart:2.5.1"
  api "com.nimbusds:nimbus-jose-jwt:9.40"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task examplesJar(type: Jar, dependsOn: test) {
    classifier = 'examples'
    from new File(buildDir, '/examples')
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
  archives javadocJar
  archives examplesJar
  archives sourcesJar
}

jar {
  manifest {
    attributes("Built-By": System.getProperty('user.name'),
              "Build-Jdk": System.getProperty('java.version'),
              "Implementation-Title": componentName,
              "Implementation-Version": project.version,
              "Implementation-Vendor-Id": project.group,
              "Implementation-Vendor": organizationName)
  }
}

publishing {
  publications {
      mavenJava(MavenPublication) {
          from components.java

          artifact javadocJar { classifier "javadoc" }
          artifact examplesJar { classifier "examples" }
          artifact sourcesJar { classifier "sources" }
          pom.withXml {
            asNode().appendNode("name", componentName)
            asNode().appendNode("description", componentDesc)
            asNode().appendNode("url", "https://interlok.adaptris.net/interlok-docs/#/pages/cookbook/cookbook-http-client?id=oauth")
            def properties = asNode().appendNode("properties")
            properties.appendNode("target", "3.6.5+")
            properties.appendNode("license", "false")
            properties.appendNode("tags", "oauth,azure")
            properties.appendNode("externalUrl", "https://github.com/AzureAD/azure-activedirectory-library-for-java")
            properties.appendNode("repository", "https://github.com/adaptris/interlok-oauth")
          }
      }
  }
  repositories {
    maven {
      credentials {
        username repoUsername
        password repoPassword
      }
      url mavenPublishUrl
    }
  }
}

delombok {
  target = delombokTargetDir
}

task deleteGeneratedFiles(type: Delete) {
  delete delombokTargetDir
}

clean.dependsOn deleteGeneratedFiles
